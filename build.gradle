import javax.tools.JavaCompiler

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.google.gms:google-services:4.3.2'
        classpath "com.airbnb.okreplay:gradle-plugin:1.4.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        // For olm library. This has to be declared first, to ensure that Olm library is not downloaded from another repo
        maven {
            url 'https://jitpack.io'
            content {
                // Use this repo only for olm library
                includeGroupByRegex "org\\.matrix\\.gitlab\\.matrix-org"
                // And also for FilePicker
                includeGroupByRegex "com\\.github\\.jaiselrahman"
                // And monarchy
                includeGroupByRegex "com\\.github\\.Zhuinden"
            }
        }
        maven {
            url "http://dl.bintray.com/piasy/maven"
            content {
                includeGroupByRegex "com\\.github\\.piasy"
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        google()
        jcenter()
        maven {
            url 'https://repo.adobe.com/nexus/content/repositories/public/'
            content {
                includeGroupByRegex "diff_match_patch"
            }
        }
    }

    tasks.withType(JavaCompile).all {
        options.compilerArgs += [
                '-Adagger.gradle.incremental=enabled'
        ]
    }

    afterEvaluate {
        extensions.findByName("kapt")?.arguments {
            arg("dagger.gradle.incremental", "enabled")
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'org.sonarqube'

// To run a sonar analysis:
// Run './gradlew sonarqube -Dsonar.login=<REPLACE_WITH_SONAR_KEY>'
// The SONAR_KEY is stored in passbolt

sonarqube {
    properties {
        property "sonar.projectName", "RiotX-Android"
        property "sonar.projectKey", "vector.android.riotx"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectVersion", project(":vector").android.defaultConfig.versionName
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.links.homepage", "https://github.com/vector-im/riotX-android/"
        property "sonar.links.ci", "https://buildkite.com/matrix-dot-org/riotx-android"
        property "sonar.links.scm", "https://github.com/vector-im/riotX-android/"
        property "sonar.links.issue", "https://github.com/vector-im/riotX-android/issues"
        property "sonar.organization", "new_vector_ltd_organization"
        property "sonar.login", project.hasProperty("SONAR_LOGIN") ? SONAR_LOGIN : "invalid"
    }
}

project(":vector") {
    sonarqube {
        properties {
            property "sonar.sources", project(":vector").android.sourceSets.main.java.srcDirs
            // exclude source code from analyses separated by a colon (:)
            // property "sonar.exclusions", "**/*.*"
        }
    }
}

//project(":matrix-sdk-android") {
//    sonarqube {
//        properties {
//            property "sonar.sources", project(":matrix-sdk-android").android.sourceSets.main.java.srcDirs
//            // exclude source code from analyses separated by a colon (:)
//            // property "sonar.exclusions", "**/*.*"
//        }
//    }
//}
//
//project(":matrix-sdk-android-rx") {
//    sonarqube {
//        properties {
//            property "sonar.sources", project(":matrix-sdk-android-rx").android.sourceSets.main.java.srcDirs
//            // exclude source code from analyses separated by a colon (:)
//            // property "sonar.exclusions", "**/*.*"
//        }
//    }
//}

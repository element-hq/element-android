// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    apply from: 'dependencies.gradle'

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        // Release notes of Android Gradle Plugin (AGP):
        // https://developer.android.com/studio/releases/gradle-plugin
        classpath libs.gradle.gradlePlugin
        classpath libs.gradle.kotlinPlugin
        classpath libs.gradle.hiltPlugin
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
        classpath "com.likethesalad.android:string-reference:1.2.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// ktlint Plugin
plugins {
    id "org.jlleitschuh.gradle.ktlint" version "10.2.0"
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    repositories {
        // For olm library. This has to be declared first, to ensure that Olm library is not downloaded from another repo
        maven {
            url 'https://jitpack.io'
            content {
                // Use this repo only for olm library
                includeGroupByRegex "org\\.matrix\\.gitlab\\.matrix-org"
                // And also for FilePicker
                includeGroupByRegex "com\\.github\\.jaiselrahman"
                // And monarchy
                includeGroupByRegex "com\\.github\\.Zhuinden"
                // And ucrop
                includeGroupByRegex "com\\.github\\.yalantis"
                // JsonViewer
                includeGroupByRegex 'com\\.github\\.BillCarsonFr'
                // PhotoView
                includeGroupByRegex 'com\\.github\\.chrisbanes'
                // PFLockScreen-Android
                includeGroupByRegex 'com\\.github\\.vector-im'
                // DraggableView
                includeGroupByRegex 'com\\.github\\.hyuwah'

                // Chat effects
                includeGroupByRegex 'com\\.github\\.jetradarmobile'
                includeGroupByRegex 'nl\\.dionsegijn'

                // Voice RecordView
                includeGroupByRegex 'com\\.github\\.Armen101'
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        // Jitsi repo
        maven {
            url "https://github.com/vector-im/jitsi_libre_maven/raw/main/android-sdk-3.1.0"
            // Note: to test Jitsi release you can use a local file like this:
            // url "file:///Users/bmarty/workspaces/jitsi_libre_maven/android-sdk-3.1.0"
        }
        google()
        mavenCentral()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        // Warnings are potential errors, so stop ignoring them
        // You can override by passing `-PallWarningsAsErrors=false` in the command line
        kotlinOptions.allWarningsAsErrors = project.getProperties().getOrDefault("allWarningsAsErrors", "true").toBoolean()
    }

    // Fix "Java heap space" issue
    tasks.withType(org.jlleitschuh.gradle.ktlint.tasks.BaseKtLintCheckTask).configureEach {
        it.workerMaxHeapSize.set("2G")
    }

    // See https://github.com/JLLeitschuh/ktlint-gradle#configuration
    ktlint {
        android = true
        ignoreFailures = false
        enableExperimentalRules = true
        // display the corresponding rule
        verbose = true
        disabledRules = [
                "spacing-between-declarations-with-comments",
                "no-multi-spaces",
                "experimental:spacing-between-declarations-with-annotations",
                "experimental:annotation"
        ]
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'org.sonarqube'

// To run a sonar analysis:
// Run './gradlew sonarqube -Dsonar.login=<REPLACE_WITH_SONAR_KEY>'
// The SONAR_KEY is stored in passbolt

sonarqube {
    properties {
        property "sonar.projectName", "Element-Android"
        property "sonar.projectKey", "im.vector.app.android"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectVersion", project(":vector").android.defaultConfig.versionName
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.links.homepage", "https://github.com/dinsic-pim/tchap-android-v2/"
        property "sonar.links.ci", "https://buildkite.com/matrix-dot-org/element-android"
        property "sonar.links.scm", "https://github.com/dinsic-pim/tchap-android-v2/"
        property "sonar.links.issue", "https://github.com/dinsic-pim/tchap-android-v2/issues"
        property "sonar.organization", "new_vector_ltd_organization"
        property "sonar.login", project.hasProperty("SONAR_LOGIN") ? SONAR_LOGIN : "invalid"
    }
}

project(":vector") {
    sonarqube {
        properties {
            property "sonar.sources", project(":vector").android.sourceSets.main.java.srcDirs
            // exclude source code from analyses separated by a colon (:)
            // Exclude Java source
            property "sonar.exclusions", "**/BugReporterMultipartBody.java"
        }
    }
}

project(":diff-match-patch") {
    sonarqube {
        skipProject = true
    }
}

//project(":matrix-sdk-android") {
//    sonarqube {
//        properties {
//            property "sonar.sources", project(":matrix-sdk-android").android.sourceSets.main.java.srcDirs
//            // exclude source code from analyses separated by a colon (:)
//            // property "sonar.exclusions", "**/*.*"
//        }
//    }
//}
//
//project(":matrix-sdk-android-rx") {
//    sonarqube {
//        properties {
//            property "sonar.sources", project(":matrix-sdk-android-rx").android.sourceSets.main.java.srcDirs
//            // exclude source code from analyses separated by a colon (:)
//            // property "sonar.exclusions", "**/*.*"
//        }
//    }
//}
